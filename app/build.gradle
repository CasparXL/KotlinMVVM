plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
}


android {
    signingConfigs {
        release {
            keyAlias 'kotlinmvvm'
            keyPassword '123456'
            storeFile file('../signature/releaseSign.jks')
            storePassword '123456'
        }
    }
    compileSdk libs.versions.compileSdk.get().toInteger()

    defaultConfig {
        applicationId libs.versions.applicationId.get()
        minSdk libs.versions.minSdk.get().toInteger()
        targetSdk libs.versions.targetSdk.get().toInteger()
        versionCode libs.versions.versionCode.get().toInteger()
        versionName libs.versions.versionName.get()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true  //开启分包
        // 使用矢量图支持库（为了兼容 API 21 以下）
        // 阿里巴巴矢量图库：https://www.iconfont.cn/
        vectorDrawables.useSupportLibrary = true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }
        defaultConfig {
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true   //开启混淆
            zipAlignEnabled true  //压缩优化
            shrinkResources true  //移出无用资源
            buildConfigField("boolean", "LOG_ENABLE", "false")//控制日志是否打印
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false   //开启混淆
            zipAlignEnabled false  //压缩优化
            shrinkResources false  //移出无用资源
            buildConfigField("boolean", "LOG_ENABLE", "true")//控制日志是否打印
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    //避免打正式包时一直下载groovy.jar的问题

    buildFeatures {
        viewBinding true
    }

    compileOptions {//使用JAVA8语法解析
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    //kotlin的java8支持
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/rxjava.properties']
        }
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
    namespace 'com.caspar.xl'
    //apk输出格式
    applicationVariants.all {
        variant ->
            // Apk 输出配置
            variant.outputs.all { output ->
                if (variant.buildType.name == buildTypes.debug.getName()) {
                    outputFileName = rootProject.getName() + '_v' + variant.versionName + '_' + variant.buildType.name + '.apk'
                } else {
                    outputFileName = rootProject.getName() + '_v' + variant.versionName + '_' + new Date().format("yyyyMMdd") + '_' + variant.buildType.name + '.apk'
                }
            }
    }
}

dependencies {
    //依赖库配置
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    // 基础库
    implementation project(':base')
    implementation project(':common')
    implementation libs.bundles.androidx.view
    implementation libs.bundles.kotlin
    implementation libs.bundles.lifecycle
    implementation libs.bundles.coroutines
    implementation libs.flycoTabLayout
    implementation libs.chart

    implementation libs.androidx.multidex
    implementation(libs.bundles.retrofit){
        exclude group: 'org.jetbrains.kotlin'
    }

    implementation libs.google.gson
    implementation libs.bundles.rxjava
    implementation libs.toast
    implementation libs.adapter
    implementation (libs.google.material) {
        exclude group: 'androidx.recyclerview'
    }
    implementation 'com.iqiyi.xcrash:xcrash-android-lib:3.0.0'
    implementation libs.mmkv
    implementation libs.bundles.room
    implementation libs.bundles.camerax
    implementation libs.bundles.smart
    implementation libs.coil
    implementation libs.lottie
    kapt libs.room.compiler
    debugImplementation libs.leakcanary
    testImplementation libs.bundles.testImplementation
    androidTestImplementation libs.bundles.androidtestImpl
}


pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}
//开启依赖管理
enableFeaturePreview('VERSION_CATALOGS')
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
    versionCatalogs {
        libs {
            version('minSdk','21')
            version('compileSdk','31')
            version('targetSdk','31')
            version('versionCode','1')
            version('versionName','1.0.0')
            version('applicationId','com.caspar.xl')

            version('kotlin','1.6.10')
            version('coroutines','1.6.0')
            version('androidx-activity','1.4.0')
            version('androidx-fragment','1.4.1')
            version('androidx-recyclerview','1.2.1')
            version('androidx-appcompat','1.4.1')
            version('androidx-constraintlayout','1.4.1')
            version('androidx-multidex','2.0.1')
            version('androidx-core-ktx','1.7.0')
            version('androidx-lifecycle','2.4.1')
            version('androidx-test','1.1.2')
            version('androidx-espresso','3.3.0')
            version('junit','4.13.2')
            version('androidx-camera-view','1.0.0-alpha17')
            version('androidx-camerax','1.0.0-beta10')
            version('google-material','1.3.0')
            version('google-gson','2.9.0')
            version('room','2.4.2')
            version('rx-android','3.0.0')
            version('rx-java','3.1.4')
            version('retrofit','2.9.0')
            version('okhttp-log','5.0.0-alpha.6')
            version('smart','2.0.5')
            version('coil','2.0.0-rc02')
            version('toast','10.3')
            version('adapter','3.0.7')
            version('mmkv','1.2.12')
            version('mqtt-client','1.1.0')
            version('mqtt-android','1.1.1')
            version('lottie','5.0.3')
            version('crash','2.3.0')
            version('leakcanary','2.8.1')

            alias('kotlin-stdlib').to('org.jetbrains.kotlin','kotlin-stdlib').versionRef('kotlin')
            alias('kotlin-reflect').to('org.jetbrains.kotlin','kotlin-reflect').versionRef('kotlin')
            alias('kotlinx-coroutines-core').to('org.jetbrains.kotlinx','kotlinx-coroutines-core').versionRef('coroutines')
            alias('kotlinx-coroutines-android').to('org.jetbrains.kotlinx','kotlinx-coroutines-android').versionRef('coroutines')
            alias('androidx-activity').to('androidx.activity','activity-ktx').versionRef('androidx-activity')
            alias('androidx-fragment').to('androidx.fragment','fragment-ktx').versionRef('androidx-fragment')
            alias('androidx-recyclerview').to('androidx.recyclerview','recyclerview').versionRef('androidx-recyclerview')
            alias('androidx-appcompat').to('androidx.appcompat','appcompat').versionRef('androidx-appcompat')
            alias('androidx-constraintlayout').to('androidx.constraintlayout','constraintlayout').versionRef('androidx-constraintlayout')
            alias('androidx-multidex').to('androidx.multidex','multidex').versionRef('androidx-multidex')
            alias('androidx-core-ktx').to('androidx.core','core-ktx').versionRef('androidx-core-ktx')
            alias('androidx-lifecycle-viewmodel').to('androidx.lifecycle','lifecycle-viewmodel-ktx').versionRef('androidx-lifecycle')
            alias('androidx-lifecycle-runtime').to('androidx.lifecycle','lifecycle-runtime-ktx').versionRef('androidx-lifecycle')
            alias('androidx-lifecycle-livedata').to('androidx.lifecycle','lifecycle-livedata-ktx').versionRef('androidx-lifecycle')
            alias('androidx-lifecycle-common').to('androidx.lifecycle','lifecycle-common-java8').versionRef('androidx-lifecycle')
            alias('androidx-lifecycle-process').to('androidx.lifecycle','lifecycle-process').versionRef('androidx-lifecycle')
            alias('androidx-camera-view').to('androidx.camera','camera-view').versionRef('androidx-camera-view')
            alias('androidx-camera-camera2').to('androidx.camera','camera-camera2').versionRef('androidx-camerax')
            alias('androidx-camera-lifecycle').to('androidx.camera','camera-lifecycle').versionRef('androidx-camerax')
            alias('androidx-test-ext').to('androidx.test.ext','junit').versionRef('androidx-test')
            alias('androidx-test-espresso').to('androidx.test.espresso','espresso-core').versionRef('androidx-espresso')
            alias('junit').to('junit','junit').versionRef('junit')
            alias('google-material').to('com.google.android.material','material').versionRef('google-material')
            alias('google-gson').to('com.google.code.gson','gson').versionRef('google-gson')
            alias('room-runtime').to('androidx.room','room-runtime').versionRef('room')
            alias('room-ktx').to('androidx.room','room-ktx').versionRef('room')
            alias('room-testing').to('androidx.room','room-testing').versionRef('room')
            alias('room-compiler').to('androidx.room','room-compiler').versionRef('room')
            alias('rx-android').to('io.reactivex.rxjava3','rxandroid').versionRef('rx-android')
            alias('rx-java').to('io.reactivex.rxjava3','rxjava').versionRef('rx-java')
            alias('retrofit').to('com.squareup.retrofit2','retrofit').versionRef('retrofit')
            alias('retrofit-converter-gson').to('com.squareup.retrofit2','converter-gson').versionRef('retrofit')
            alias('retrofit-converter-scalars').to('com.squareup.retrofit2','converter-scalars').versionRef('retrofit')
            alias('okhttp-log').to('com.squareup.okhttp3','logging-interceptor').versionRef('okhttp-log')
            alias('smart-kernel').to('io.github.scwang90','refresh-layout-kernel').versionRef('smart')
            alias('smart-header').to('io.github.scwang90','refresh-header-material').versionRef('smart')
            alias('smart-footer').to('io.github.scwang90','refresh-footer-classics').versionRef('smart')
            alias('coil').to('io.coil-kt','coil').versionRef('coil')
            alias('toast').to('com.github.getActivity','ToastUtils').versionRef('toast')
            alias('adapter').to('com.github.CymChad','BaseRecyclerViewAdapterHelper').versionRef('adapter')
            alias('mmkv').to('com.tencent','mmkv').versionRef('mmkv')
            alias('mqtt-client').to('org.eclipse.paho','org.eclipse.paho.client.mqttv3').versionRef('mqtt-client')
            alias('mqtt-android').to('org.eclipse.paho','org.eclipse.paho.android.service').versionRef('mqtt-android')
            alias('lottie').to('com.airbnb.android','lottie').versionRef('lottie')
            alias('crash').to('cat.ereza','customactivityoncrash').versionRef('crash')
            alias('leakcanary').to('com.squareup.leakcanary','leakcanary-android').versionRef('leakcanary')

            //kotlin基础组件
            bundle('kotlin',['kotlin-stdlib','kotlin-reflect'])
            //kotlin协程相关
            bundle('coroutines',['kotlinx-coroutines-core','kotlinx-coroutines-android'])
            //androidx生命周期以及试图core相关
            bundle('androidx-view',['androidx-core-ktx','androidx-activity','androidx-fragment','androidx-recyclerview','androidx-appcompat','androidx-constraintlayout'])
            //lifecycle相关
            bundle('lifecycle',['androidx-lifecycle-viewmodel','androidx-lifecycle-runtime','androidx-lifecycle-livedata','androidx-lifecycle-common','androidx-lifecycle-process'])
            //camerax相关
            bundle('camerax',['androidx-camera-view','androidx-camera-camera2','androidx-camera-lifecycle'])
            //google组件相关
            bundle('google',['google-material','google-gson'])
            //room相关
            bundle('room',['room-runtime','room-ktx'])
            //rxjava相关
            bundle('rxjava',['rx-java','rx-android'])
            //retrofit相关
            bundle('retrofit',['retrofit','retrofit-converter-gson','retrofit-converter-scalars','okhttp-log'])
            //下拉刷新相关
            bundle('smart',['smart-kernel','smart-header','smart-footer'])
            //androidTestImplementation引入方式的测试包
            bundle('androidtestImpl',['androidx-test-ext','androidx-test-espresso'])
            //testImplementation引入方式的测试包
            bundle('testImplementation',['junit','room-testing'])
            //annotationProcessor or kapt引入方式的测试包
            bundle('kapt',['room-compiler'])
        }
    }
}


rootProject.name='KotlinMVVM'
include ':app'
include ':base'
include ':common'
